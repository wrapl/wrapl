MOD Response;

IMP .Type;

DEF T! <- <[Type.T]
    namespace, name,
    items,
    ctype, rtype,
    headers, libraries,
    gtype
>;

DEF New!(namespace, name, cname, gname) (
    VAR type <- T();
    type:namespace <- namespace;
    type:name <- name;
    type:cname <- cname;
    type:gname <- gname;
    type:items <- [];
    type:ctype <- cname;
    type:rtype <- 'Gir${namespace}${name}$T';
    type:headers <- {'Gir/{namespace}/{name}.h'};
    type:libraries <- {'Gir/{namespace}/{name}.rlib'};
    type:gtype <- '{namespace:lower}_{name:lower}_get_type';
    
    Type.Define(name, type);
    
    RET type;
);

TO :riva_to_c(type@T, in, out, tmp) ['{out} = ((Gir${type:namespace}${type:name}$t *){in})->Value;'];
TO :c_to_riva(type@T, in, out, tmp) [
    'Gir${type:namespace}${type:name}$t *{tmp}_value = Riva$Memory$alloc(sizeof(Gir${type:namespace}${type:name}$t));',
    '{tmp}_value->Type = {type:rtype};',
    '{tmp}_value->Value = {in};',
    '{out} = {tmp}_value;'
];

TO :base_headers(type@T) type:headers + {'Gir/GObject/Enum.h'};
TO :base_libraries(type@T) type:libraries + {'Gir/GObject/Enum.rlib'};

TO :generate_member(type@T, name, value)
'#ifdef DOCUMENTING
GLOBAL(T, int, {name});
#else
Gir${type:namespace}${type:name}$t __{name}[] = \{\{T, {value}, {type:gtype}}};
#endif';

TO :generate_c(type@T) [
    "",
    "TYPE(T, Gir$GObject$Enum$T, Std$Integer$SmallT, Std$Integer$T, Std$Number$T);"
];

TO :generate_h(type@T) [
    'typedef struct Gir${type:namespace}${type:name}$t \{',
    '\tStd$Type$t *Type;',
    '\t{type:ctype} Value;',
    '\tGType (*GetType)();',
    '} Gir${type:namespace}${type:name}$t;',
    '',
    'extern Std$Type$t Gir${type:namespace}${type:name}$T[];'
];

TO :generate_rdef(type@T) [
    'export(\"T\")'
];

TO :generate_rlib(type@T) [
    'export(\"T\")'
];

END Response.
