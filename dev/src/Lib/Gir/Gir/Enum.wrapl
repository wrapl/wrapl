MOD Enum;

IMP .Type, .Common;

DEF T! <- <[Type.T]
    namespace, name,
    items,
    ctype, rtype,
    headers, libraries
>;

DEF New!(namespace, name, cname) (
    VAR type <- T();
    type:namespace <- namespace;
    type:name <- name;
    type:cname <- cname;
    type:items <- [];
    type:ctype <- cname;
    type:rtype <- 'Gtk${namespace}${name}$T';
    type:headers <- {'Gtk/{namespace}/{name}.h'};
    type:libraries <- {'Gtk/{namespace}/{name}.rlib'};
    
    Type.Define(cname, type);
    
    RET type;
);

TO :riva_to_c(type@T, in, out, tmp) ['{out} = ((Gtk${type:namespace}${type:name}_t *){in})->Value;'];
TO :c_to_riva(type@T, in, out, tmp) [
    'Gtk${type:namespace}${type:name}_t *{tmp}_value = Riva$Memory$alloc(sizeof(Gtk${type:namespace}${type:name}_t));',
    '{tmp}_value->Type = {type:rtype};',
    '{tmp}_value->Value = {in};',
    '{out} = {tmp}_value;'
];

TO :base_headers(type@T) type:headers;

TO :generate_member(type@T, name, value)
'#ifdef DOCUMENTING
GLOBAL(T, Gtk${type:namespace}${type:name}_t, {name});
#else
Gtk${type:namespace}${type:name}_t __{name}[] = \{\{T, {value}}};
#endif';

TO :generate_c(type@T) (
	VAR r <- [
	    "",
    	"TYPE(T, Std$Integer$SmallT, Std$Integer$T, Std$Number$T);"
	];
	WITH item <- type:items:values \ (?$ == Common.FunctionT & $:name = "GetType" & $:type = "GType" & $:params:length = 0) DO (
		r:push("#include <Util/TypedFunction.h>", "#include <Gtk/GObject/Value.h>");
		r:put("",
			'TYPED_INSTANCE(void, Gtk$GObject$Value$to_value, T, Gtk${type:namespace}${type:name}_t const *Source, GValue *Dest) \{',
			'\tg_value_init(Dest, {item:cname}());',
			'\tg_value_set_enum(Dest, Source->Value);',
			"}"
		);
	);
	RET r;
);

TO :generate_h(type@T) [
    'typedef struct Gtk${type:namespace}${type:name}_t \{',
    '\tStd$Type_t *Type;',
    '\t{type:ctype} Value;',
    '} Gtk${type:namespace}${type:name}_t;',
    '',
    'extern Std$Type_t Gtk${type:namespace}${type:name}$T[];'
];

TO :generate_rdef(type@T) [
	'library(\"Gtk/GObject/Value.rlib\")',
    'export(\"T\")'
];

TO :generate_rlib(type@T) [
    'export(\"T\")'
];

END Enum.
